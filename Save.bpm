import "LCDRu"

string[] TableRec     ' массив строк для таблицы рекордов
string NameFile       ' Имя файла с таблицей рекордов
number FTableRec      ' дескриптор файла (расширение .bpi - позволяет посмотреть содержимое файл в Clever)

number MaxLen         ' Максимальная длина таблицы (количество строк 99)
number TekLen         ' Текущая длина таблицы (количество строк занятых реальными игроками)
number MaxRecOne      ' Максимальное число рекордов для одного игрока (пока попробуем 5))

number TekNum         ' номер строки таблицы для текущего игрока (обводим двойной рамкой)
string TekName        ' Имя текущего игрока,
number TekCount       ' его рекорд,
number TekUroven      ' уровень его рекорда
string TekParol       ' и его пароль, по которому можно выбрать игрока из таблицы, а также поменять (отредактировать) своё имя и пароль в таблице рекордов

number TekStr         ' текущая строка таблицы рекордов. Выделяется одиночной рамкой. Можно выбрать стрелками вверх "U" и вниз "D"
number TekStart       ' текущая строка таблицы рекордов, отображаемая в верхней строке очередной страницы таблицы из 4 строк

string TekDate        ' для ввода даты
number dd             ' и ее распаковки день
number mm             ' месяц
number yy             ' год

' --------------------------------------------------
' считываем текущую таблицу рекордов из флешпамяти EV3 (фактически инициализация таблицы)
' Таблица всегда имеет максимальное число строк (99)
' Но число реальных игроков задется в первой строке файла 
Function ReadTableRec(in string FileName)
  NameFile  = FileName
  FTableRec = EV3File.OpenRead(NameFile+".bpi")   ' Открываем таблицу рекордов 
  MaxLen    = 99        ' максимальная длина таблицы рекордов [константа] 99?
  MaxRecOne = 5         ' максимальное число рекордов от одного игрока [константа]
  TekDate   = " "       ' Текущая дата - задаем один раз при выборе 1-го игрока 
  If FTableRec = 0 Then ' если нет таблицы рекордов - то создаем пустую
    TekLen = 0          ' задаем число реальных игроков - пока 0
    ' строки заполняем приглашением "новый игрок", чтобы новый игрок мог ввести себя в таблицу рекордов
    TekName   = "/0Yjdsq buhjr"     ' имя игрока "Новый игрок"
    TekDate   = "01.01.25"          '
    TekCount  = 0                   ' счет
    TekUroven = 1                   ' уровень
    TekParol  = ""                  ' пароль
    For TekNum = 1 To MaxLen
      SetNewStr()                   ' записываем строку таблицы 
    EndFor
    ' ------------
    SaveTableRec()        ' записываем пучтую сформированную таблицу в флешпамять EV3
  Else                    ' Иначе считываем таблицу рекордов
    Len = EV3File.ReadLine(FTableRec)
    TekLen = EV3File.ConvertToNumber(Len)
    For I = 1 To 2*MaxLen
      TableRec[I] = EV3File.ReadLine(FTableRec)
    EndFor
  EndIf
  EV3File.Close(FTableRec)
  TekNum = 0                           ' пока текущий игрок не выбран
EndFunction

' --------------------------------------------------
' записываем текущую таблицу рекордов во флешпамять EV3
Function SaveTableRec()
  FTableRec = EV3File.OpenWrite(NameFile+".bpi")
  EV3File.WriteLine(FTableRec, TekLen)
  MaxLen = 99
  For I = 1 To MaxLen
    EV3File.WriteLine(FTableRec, TableRec[2*I-1])
    EV3File.WriteLine(FTableRec, TableRec[2*I])
  EndFor
  EV3File.Close(FTableRec)
EndFunction

' --------------------------------------------------
' Запросить ввод пароля игроком и проверить совпадение с паролем из строки, по номеру заданному в numIn
' Выводим флаговую переменную isParolTrue (1 - пароль верный, 0 - пароли не совпали) 
Function TestParol(in number numIn, out number isParolTrue) 
  LCDRu.Klav("/0Ddtlbnt gfhjkm/a:", Parol)       ' просим игрока ввести пароль (с подсказкой "Введите пароль:")
  GetParol(NumIn, TabParol)                      ' считываем из таблицы скрытый пароль игрока.
  'LCDRu.TextRu(1, 0, 30, 2, "!" + TekNum + "!" + Parol + "=" + TekParol + "!")
  'Buttons.Wait()
  'Buttons.Flush()
If TabParol = Parol Then
    isParolTrue = 1                              ' пароль верный (таблицу не перерисовываем)
  Else
    isParolTrue = 0                              ' пароль неправильный
    LCD.Clear()                                  ' Сообщаем об этом
    '                       123  4  5678901   
    LCDRu.TextRu(1, 0, 20, 2, "EDS/a,/r GFHJKM") ' УВЫ, ПАРОЛЬ
    LCDRu.TextRu(1, 0, 50, 2, " YTDTHYSQ ")      ' НЕВЕРНЫЙ
    Program.Delay(1500)                          ' задержка, достаточная чтобы прочитать сообщение
    PrintTableHead()                             ' перерисовываем заголовок таблицы
    PrintTable(NumIn, 1)                         ' и перерисовываем таблицу рекордов
    'Buttons.Flush()                             ' Сбрасываем нажатые кнопки
  EndIf
  ' для отладки
  'LCDRu.TextRu(1, 0, 10, 1, TekParol)
  'LCDRu.TextRu(1, 0, 30, 1, Parol)
  'LCDRu.TextRu(1, 0, 50, 1, "" + isParolTrue)
  'Buttons.Wait()
  'Buttons.Flush()
EndFunction

' --------------------------------------------------
' Получить в NameOut имя игрока из строки таблицы рекордов, по номеру заданному в numIn
Function GetName(in number numIn, out string NameOut)
  NameOut = TableRec[2*NumIn-1]  
EndFunction

' --------------------------------------------------
' Записать в строку таблицы рекордов по номеру строки, заданной в numIn новое имя игрока NameIn)
Function SetName(in number numIn, in string NameIn)
    TableRec[2*numIn-1] = NameIn
EndFunction
  
' --------------------------------------------------
' Получить в ParolOut пароль игрока из строки таблицы рекордов, по номеру заданному в numIn
Function GetParol(in number numIn, out string ParolOut) 
  ParolOut = Text.GetSubTextToEnd(TableRec[2*NumIn], 23)
  'LCDRu.TextRu(1, 0, 10, 2, numIn + "!" + TekNum + "!" + ParolOut + "=" + TekParol + "!")
  'Buttons.Wait()
  'Buttons.Flush()
EndFunction

' --------------------------------------------------
' Записать в строку таблицы рекордов по номеру строки, заданной в numIn новый пароль игрока ParolIn)
Function SetParol(in number numIn, in string parolIn)
  TableRec[2*NumIn] = Text.GetSubText(TableRec[2*NumIn], 1, 22) + ParolIn
EndFunction
  
' --------------------------------------------------
' Получить в CountOut счет игрока из строки таблицы рекордов, по номеру заданному в numIn
Function GetCount(in number numIn, out number CountOut)
  CountOut = EV3File.ConvertToNumber(Text.GetSubText(TableRec[2*NumIn], 12, 7))
EndFunction

' --------------------------------------------------
' Получить в UrovenOut уровень игрока из строки таблицы рекордов, по номеру заданному в numIn
Function GetUroven(in number numIn, out number UrovenOut)
  UrovenOut = EV3File.ConvertToNumber(Text.GetSubText(TableRec[2*NumIn], 20, 2))
EndFunction

' --------------------------------------------------
' Записать в строку таблицы рекордов по номеру строки, новую строку с данными текущего игрока
' TekName, TekCount, TekUroven, TekParol
Function SetNewStr()
  SetName(TekNum, TekName)               ' Записываем имя игрока
  NumToStrRight(TekCount, 7, strCount)   ' выравниваем счет вправо
  NumToStrRight(TekUroven, 2, strUroven) ' выравниваем уровень вправо
  TableRec[2*TekNum] = TekDate + " - " + strCount + ":" + strUroven + "L" + TekParol
EndFunction

' --------------------------------------------------
' Перевод числа в строку с выровниванием вправо в заданное число позиций. 
' Заданное число numberIn, число позиций posIn, вывод в строку strOut)
Function NumToStrRight(in number numberIn, in number posIn, out string strOut)
  Tnumber  = "" + numberIn               ' перевод в строковый вид
  LenNum = Text.GetLength(Tnumber)       ' вычисляем число позиций числа
  strOut = ""                            '
  For I = 1 To posIn - LenNum            ' добавляем подводящие пробелы, чтобы отформатировать счет вправо
    strOut = strOut + " "                '
  EndFor
  strOut = strOut + numberIn             ' формируем выравненное вправо число
EndFunction

' --------------------------------------------------
' распечатываем текущий заголовок таблицы рекордов
Function PrintTableHead()
  LCDRu.StopUpdate()
  LCDRu.Clear()                                         ' Все таки нужно очистить экран
  LCDRu.TextRu(1, 0, 10, 1, "   NF<KBWF HTRJHLJD   ")   ' заголовок таблицы
  LCDRu.Rect(1, 0, 5, 176, 18)                          ' двойная рамка вокруг заголовка
  LCDRu.Rect(1, 3, 7, 170, 14)   
  LCDRu.Update()
EndFunction

' --------------------------------------------------
' распечатываем текущую страницу таблицы рекордов
' Число строк в отображаемой странице не должно быть больше 5. Скролинг таблицы осуществляется по значению текущей строки
'  
Function PrintTable(in number numIn, in number prizDraw)
  LCDRu.StopUpdate()
  GetIndexTek(numIn, priz)                                      ' Вычисляем номер строки в первой строке экрана
  If priz = 1 Or prizDraw = 1 Then                              ' надо распечатывать строки (1) или надо просто переставить курсор (0)
    For I = 1 To 5                                              ' Распечатываем строки таблицы
      If I + TekStart < TekLen + 3 Then                         ' Строки таблицы с реальными рекордами - выводим
        GetNum(I + TekStart - 1, numStr)                        ' номер строки в текстовый вид с ")"                   
        LCD.Text(1, 24, I*20+4,  1, "                    ")     ' очищаем строку с предыдущим именем (оно может быть длинее выводимого)
        LCDRu.TextRu(1, 0,  I*20+4,  1, numStr)                 ' выводим номер рекоррда = номер строки в таблице рекордов
        LCDRu.TextRu(1, 24, I*20+4,  1, TableRec[2*(I+TekStart-1)-1])' затем выводим имя из строки таблицы
        LCDRu.Text(1, 0,  I*20+14, 1, TableRec[2*(I+TekStart-1)])    ' затем дату, очки и уровень на следующей строке
        If TekNum = I + TekStart - 1 Then                       ' текущая строка - это строка текущего игрока?       
          LCD.FillRect(1, 17,  I*20+4,  4, 15)                  ' для текущего игрока ставим отметку - курсор высотой в две строки
        EndIf  
      Else  
        LCD.Text(1, 0, I*20+4,  1, "                      ")    ' очищаем строку
        LCD.Text(1, 0, I*20+14, 1, "                      ")    ' таблицы
      EndIf                                                     '
    EndFor                                                      '
  EndIf                                                         '   
  LCDRu.Update()                                                ' одновременный вывод сформированного буфера на экран
  SetFrame(numIn)                                               ' Выводим рамку вокруг текущая выбранная строки
  'LCDRu.TextRu(1, 0, 10, 1, numIn + "!" + num  + "!"  + TekNum + "!" + TekLen + "!" + TekParol)
EndFunction

' --------------------------------------------------
' Вычисляем индекс (номер строки таблицы) выводимый в первой строке очередной порции таблицы рекордов из 5 строк
' в скроллинге таблицы рекордов для отображения на экране EV3. Номер текущей строки задан в numIn
' Устанавливаем признак скроллинга = 1, если надо переместить строки вверх или вниз. Иначе просто переставляем рамку
' При скроллинге также изменяем глобальную переменную TekStart - номер строки отображаемой вверху первой на странице
Function GetIndexTek(in number numIn, out number prizDraw)
  If     numIn = TekStart - 1 Then        ' Если текущая строка при последовательном скроллинге вышла за верхную границу текущей страницы
         TekStart = TekStart - 1          ' то уменьшаем номер верхней отображаемой строки (курсор останеться на верхней 1 строке)
         prizDraw = 1                     ' то делаем скроллинг страницы вниз
  ElseIf numIn >= TekStart And numIn < TekStart + 5 Then        ' текущая строка находится внутри страницы
         prizDraw = 0                     ' перерисовки страницы (скроллинг_ )не делаем - просто переставляем рамку курсора
  ElseIf numIn = TekStart + 5 Then        ' Если текущая строка при последовательном скроллинге вышла за нижную границу текущей страницы 
         TekStart = TekStart + 1          ' Увеличили номер верхней отображаемой строки (курсор останеться на последней 5 строке)
         prizDraw = 1                     ' то делаем скроллинг страницы вниз
  Else                                    ' Первый вывод текущей страницы таблицы (то есть нет последовательного скроллинга)
         prizDraw = 1                     ' Строки следует распечатать, так как это первая отображаемая страница
         If numIn < 4 Then                ' Для первых 3 строк страницы   
            TekStart = 1                  ' курсор ставим на текущей строке, а страницу отображаем с 1 строки
         Else                             ' Для всех остальных строк 
            TekStart = numIn - 2          ' Страницу начинает с такой строки, чтобы курсор, стоящий на текущей строке, был на 3 строке страницы (посредине) 
         EndIf   
  EndIf   
EndFunction

' --------------------------------------------------
' Получить в numOut номер строки таблицы рекордов в виде "n) " или "nn)", по номеру заданному в numIn
Function GetNum(in number numIn, out string numOut)
  If numIn <= 9 Then
    numOut = "" + numIn + ") "
  Else
    numOut = "" + numIn + ")"
  EndIf
EndFunction

' --------------------------------------------------
' Задать рамку вокруг текущей строки таблицы рекордов. Номер строки в numIn
Function SetFrame(in number numIn)
  GetPosTek(numIn, pos)  
  If pos > 0 Then                           ' на всякий случай, вдруг как то текущая строка вышла за пределы страницы  
    LCDRu.Rect(1, 0, pos*20+3, 175, 20)     ' Задаем одиночную рамку вокруг текущей строки таблицы рекордов
    If TekNum = numIn Then
      LCDRu.Rect(1, 1, pos*20+4, 174, 18)   ' Задаем двойную рамку вокруг строки текущего игрока таблицы рекордов
    EndIf
  EndIf  
EndFunction

' --------------------------------------------------
' Убрать рамку вокруг текущей строки таблицы рекордов. Номер строки в numIn
Function ClearFrame(in number numIn)
  GetPosTek(numIn, pos)
  If pos > 0 Then                                               ' на всякий случай, вдруг как то текущая строка вышла за пределы страницы  
    LCDRu.Rect(0, 0, pos*20+3, 175, 20)                         ' Убираем одиночнцю рамку вокруг текущей строки таблицы рекордов
    If TekNum = numIn Then                                      ' Для текущего игрока
      LCDRu.Rect(0, 1, pos*20+4, 174, 18)                       ' Убираем двойную рамку вокруг строки текущего игрока таблицы рекордов
      ' и перерисовываем строку текущего игрока, так как внутренняя рамка ее подпортила сверху и снизу
      GetNum(numIn, numStr)                                     ' номер строки в текстовый вид с ")"                   
      LCDRu.TextRu(1, 0,  pos*20+4,  1, numStr)                 ' перевыводим номер рекорда = номер строки в таблице рекордов
      LCDRu.TextRu(1, 24, pos*20+4,  1, TableRec[2*(numIn)-1])  ' перевыводим имя из строки таблицы
      LCD.Text(1, 0,  pos*20+14, 1, TableRec[2*numIn])          ' затем дату, очки и уровень на следующей строке
      LCD.FillRect(1, 17,  pos*20+4,  4, 15)                      ' для текущего игрока ставим отметку - курсор высотой в две строки
    EndIf
  EndIf  
EndFunction

' --------------------------------------------------
' Вычисляем позицию текущей строки в скроллинге таблицы рекордов на экране EV3. Номер текущей строки задан в numIn 
' в начале таблицы для первых 5 строк - = numIn, далее 5 строка, пока меньше 95
' в последнем участке (numIn > 94) - = numIn - 94   
Function GetPosTek(in number numIn, out number posOut)
  If numIn >= TekStart And numIn < TekStart + 5 Then       ' Текущая строка должна попадать в текущую отображаемую страницу
    posOut = numIn - TekStart + 1                          ' Вычисляем номер строки курсора в текущей странице
  Else                                                     ' вообще то так быть не должно (текущая строка всегда должна быть в текущей странице),
    posOut = 0                                             ' но на всякий случай задаем признак - не рисовать рамку вокруг текушей строки
  EndIf   
EndFunction

' --------------------------------------------------
' Распечатываем таблицу для ее редактирования или просмотра
' отображать таблицу начинаем так, чтобы текущая строка с номером TekNum попадала в отображение, причем
' так, чтобы она отображалась на предпоследней строке, если у нее номер больше равен 4 и если всего строк больше 4
' Всегда отображаем по пять строк таблицы рекордов (каждая строка занимает по 2 текстовых строки на экране)
' Последний отображаемый участок от MaxLen - 5 (95) строки вверху, то есть в таблице всегда будет отображаться по пять строк.
' Но переход за TekLen (текущее количество реальных игроков) по кнопке "D" будет только на 1 строку вниз, так,
' чтобы подсказка по вводу нового игрока отображалась на последней строке, ну кроме последнего участка.       
' Если задан только просмотр (priz = 0), то работают только кнопки "U" и "D", текущую строку TekNum не изменяем и не выбираем,
' исправить имя или задать пароль не можем. По кнопке "E" просто возвращаемся назад.
' Если задан режим редактирования, то мы обязательно должны выбрать нового текущего игрока (по нажатию на кнопку "E") - это может быть уже записанный игрок,
' но тогда он должен ввести свой пароль, либо для новый игрок, тогда он должен ввести свое имя и задать свой пароль
' При первом обращении в таблицу за определением текукщего игрока - программа потребует ввести текущую дату, которой будет маркировать рекорды.  
Function PrintTableRec(in number priz)
  TekStart = MaxLen + 5                      ' Заведомо ставим сверхмаксимальное значение, чтобы было TekStr < TekStart - признак вывода первой страницы 
  If TekNum = 0 Then                         ' Первое обращение к выводу таблицы
    If priz = 0 Then                         ' Если не задано выбирать игрока,
      TekStr = 1                             ' то просто начинаем сверху таблицы
    Else                                     ' Если задано выбрать игрока
      TekNum = 1                             ' То задаем текущего игрока - чемпион из верхней строчки
      TekStr = 1                             ' и тоже отображаем верх таблицы
    EndIf                                    '
    If TekDate = " " Then                    ' Если при первом обращении к таблице рекордов дата не введена,
      dd = 1                                 '
      mm = 1                                 '
      yy = 25                                '
      EnterDateRec(0)                        ' mо вводим текущую дату
    EndIf                                    '
  Else                                       ' Если текущий игрок задан, то будем отображать ту часть таблицы, где он находится.
    TekStr = TekNum                          ' текущая строка таблицы - есть номер строки с текущим игроком
  EndIf  
  btn = ""                                   ' код нажатой кнопки
  Buttons.Flush()                            ' сбрасываем буфер нажатых кнопок
  PrintTableHead()                           ' Выводим заголовок таблицы
  While btn <> "E"                           ' Циклим, до тех пор, пока не нажмем "E"
    PrintTable(TekStr, 0)                    ' Распечатали пять очередных строк скроллинга таблицы рекордов 
    Buttons.Wait()                           ' Ждем команды от игрока (нажатие кнопки)
    btn = Buttons.GetClicks()                ' вводим код нажатой кнопки
    ClearFrame(TekStr)                       ' Убрали рамку вокруг выбранной строки
    If btn = "U" Then                        ' текущая строка вверх
      If TekStr > 1 Then                     ' до 1
        TekStr = TekStr - 1                  ' переходим на предыдущую строку
      EndIf                                  '
    ElseIf btn = "D" Then                    ' текущая строка вниз
      If TekStr < TekLen + 1 Then            ' Дальше первой пустой строки не передвигаемся     
        TekStr = TekStr + 1                  ' Переходим на следующую строку
      EndIf
    ElseIf btn = "L"  And priz = 1 Then      ' Изменить имя игрока
      If Text.StartsWith(TekName, "/0") Then ' Для пустой строки имя и пароль не меняем
      Else                                   ' а для строки с реальным рекордом и игроком - меняем
        TestParol(TekStr, isParolTrue)       ' Просим ввести пароль и сразу его проверяем с паролем текущей строки
        If isParolTrue = 1 Then              ' Если он правильный, то
          GetName(TekStr, Name)              ' Текущее имя игрока из строки таблицы рекордов
          LCDRu.Klav(Name, TabName)          ' редактируем
          SetName(TekStr, TabName)           ' Заносим его в нашу таблицу
          SaveTableRec()                     ' запоминаем в файле                                              
          PrintTableHead()                   ' перерисовываем заголовок таблицы
          PrintTable(TekStr, 1)              ' надо перерисовать таблицу рекордов с новым именем игрока
        EndIf        
      EndIf        
    ElseIf btn = "R" And priz = 1  Then      ' Изменить пароль игрока
      If Text.StartsWith(TekName, "/0") Then ' Для пустой строки имя и пароль не меняем
      Else                                   ' а для строки с реальным рекордом и игроком - меняем
        TestParol(TekStr, isParolTrue)       ' Просим ввести старый пароль из текущей строки с проверкой
        If isParolTrue = 1 Then              ' Если он правильный, то
          S = "/0Ddtlbnt yjdsq gfhjkm"       ' "введите свой пароль"
          LCDRu.Klav(S, TabParol)            ' просим ввести новый пароль
          SetParol(TekStr, TabParol)         ' заносим новый пароль в нашу таблицу
          SaveTableRec()                     ' запоминаем в файле                                              
          PrintTableHead()                   ' перерисовываем заголовок таблицы
          PrintTable(TekStr, 1)              ' надо перерисовать таблицу рекордов с новым именем игрока
        EndIf        
      EndIf        
    ElseIf btn = "E" And priz = 1 Then       ' Выбор текущего игрока и выход из вывода таблицы рекордов
      GetName(TekStr, TabName)               ' Выбираем имя игрока из текущей строки таблицы рекордов
      If Text.StartsWith(TabName, "/0") Then ' Если выбранная строка пустая и содерит приглашение /0 Новый игорок
        S = "/0Dtlbnt cdj` bvz"              ' То выводим подсказка "Введите своё имя" 
        LCDRu.Klav(S, TekName)               ' вводим имя нового игрока, так как оно не задано
        TekCount = 0                         ' рекорд пока что 0 - заготовка для рекорда
        TekUroven = 1                        ' уровень 1
        S = "/0Pflfqnt gfhjkm"               ' просим задать 
        LCDRu.Klav(S, TekParol)              ' пароль
        TekNum = TekStr                      ' Задаем номер строки с текущим игроком
        SetNewStr()                          ' Заносим нового игрока в строку TekNum таблицы рекордов
        If TekLen < MaxLen - 2 Then          ' 
           TekLen += 1                       ' таблица не исчерпана - то увеличиваем число реальных записей.
        EndIf 
        SaveTableRec()                       ' запоминаем в файле изменения в таблице
        'PrintTableHead()                     ' перерисовываем заголовок таблицы (лишнее - сразу переходим к приветствию уже нового игрока))
        'PrintTable(TekNum, 1)                ' надо перерисовать таблицу рекордов с новым именем игрока
      Else                                   ' если выбран уже существующий игрок, то 
        TestParol(TekNum, isParolTrue)       ' Просим ввести пароль и сразу его проверяем с паролем текущей строки
        If isParolTrue = 1 Then              ' Если пароль верный, то считываем параметры текущего игрока
          TekNum = TekStr                    ' то задаем номер строки с текущим игроком и вводим из строки таблицы рекордов:
          GetName(TekNum, TekName)           ' имя текущего игрока,
          GetCount(TekNum, TekCount)         ' его рекордный счет,
          GetUroven(TekNum, TekUroven)       ' и его уровень,
          GetParol(TekNum, TekParol)         ' так же и пароль (для запоминания в строку таблицы при записи нового рекорда)
          'break                             ' и выходим (а в принципе и так выйдем - по while)                             
        Else                                 ' TekNum - остается прежним, то есть не меняется на TekStr
          btn = ""                           ' Если пароль не правильный, то не реагируем на нажатие кнопки "E"
        EndIf
      EndIf                                  '
    EndIf
  EndWhile
EndFunction

' --------------------------------------------------
' проверяем текущий счет в countIn на наличие рекорда текущего игрока (TekNum) в таблице рекордов 
' возвращаем признак isRecord - есть рекорд и записываем рекорд в таблицу рекордов
Function PushTableRec(in number CountIn, in number UrovenIn, out number IsRecord)
  isRecord  = 0                     ' Сбросили флаг
  numRec    = 0                     ' счетчик рекордов текущего игрока
  numRecEnd = 0                     ' для запоминания номера записи с последним рекордом текущего игрока
  CountMin  = 0                     ' последний - то есть самый минимальный рекорд текущего игрока, допустимый по лимиту записей для одного игрока
  For I = 1 To TekLen + 1           ' только для строк с реальными рекордаим
    GetCount(I, CountTab)           ' считали рекорд из строки таблицы
    GetName(I, NameTab)             ' и имя рекордсмена
    If TekName = NameTab Then
      numRec += 1                   ' увеличили счетчик записанных рекордов текущего игрока
      If numRec = MaxRecOne Then    ' Запомним номер строки таблицы рекордов
        numRecEnd = I               ' с последним максимально допустимым рекордом текущего игрока,
        CountMin = CountTab         ' эту строку и будем удалять, если текущий рекорд больше последнего рекорда игрока
      EndIf
    EndIf
    If isRecord = 0 Then            ' Если рекорд ещё не был зафиксирован, то проолжаем проверку 
      If CountIn > CountTab Then    ' проверяем заданный рекорд больше или равен чем в строке таблицы
        IsRecord = 1                ' если да - то ставим признак и
        Index = I                   ' запоминаем в Index номер строки таблицы куда будем вставлять наш рекорд
      EndIf
    EndIf                           ' иначе - просто досчитываем количество возможных рекордов текущего игрока
  EndFor
  ' Если рекорд зафиксирован, то надо его записать в таблицу
  If isRecord = 1 Then              ' 
    If numRecEnd > 0 Then           ' лимит записей для текущего игрока исчерпан 
      If CountMin < CountIn Then    ' и новый рекорд больше, чем последний рекорд текущего игрока?
        For I = numRecEnd To TekLen                 ' затираем строку таблицы с последним рекордом игрока (то есть удаляем его)
          TableRec[2*I - 1] = TableRec[2*(I+1) - 1] 'Смещая строки вверх  
          TableRec[2*I]     = TableRec[2*(I+1)]     'TekLen+1 - указывает на строку с приглашением ввода нового игрока 
        EndFor
        TekLen = TekLen - 1         ' Уменьшаем число записей с реальными рекордами (мы же удалили такую строку)
      Else                          ' иначе - просто игнорируем текущий рекорд текущего игрока,
        isRecord = 0                ' так как все уже записанные рекорды этого игрока больше текущего записываемого. 
      EndIf                         ' 
    EndIf                           ' 
    ' проверяем исчерпание таблицы, учитывая что последняя строка MaxLen должна собержать приглашение "Новый игрок"
    If isRecord = 1 Then              ' но если рекорд нужно записывать
      If TekLen < MaxLen - 2 Then       ' 
          TekLen += 1                   ' таблица не исчерпана - то увеличиваем число реальных записей.
      ElseIf Index < TekLen + 1 Then    ' Если таблица исчерпана, то если вставляем выше последней строки,
          TableRec[2*MaxLen - 1] = TableRec[2*(MaxLen+1) - 1] 'Вытираем самый последний рекорд 
          TableRec[2*MaxLen]     = TableRec[2*(MaxLen+1)]     'с предпоследней строки таблицы
      EndIf 
    ' Записываем в таблицу новый рекорд
      TekNum = Index                                    ' Сместились на строку с текущим рекордом
      TekCount = CountIn                                ' перезадали текущий рекорд текущего игрока
      TekUroven = UrovenIn                              ' и его текущий уровень рекорда
      For I = TekLen + 1 To TekNum+1 Step -1            ' освобождаем строку таблицы рекордов
        ' опускаем вниз те рекорды, которые превысили
        TableRec[2*I - 1] = TableRec[2*(I-1) - 1]       ' смещая все записи после нее  
        TableRec[2*I]     = TableRec[2*(I-1)]           ' вниз по таблице
        ' для отладки
        'PrintTableRec(0, 1)
        'LCD.Clear()
        'LCDRu.TextRu(1, 0, 10, 2, I + "!" + TekLen +"!" + TekCount + "!" + TekNum + "!" + TekName)
      EndFor
      SetNewStr()                                       ' Заносим рекорд в таблицу рекордов в строку TekNum
      SaveTableRec()                                    ' записываем текущую таблицу рекордов в файл
    EndIf
  EndIf
EndFunction
  
'---------------------------------------------------------------------------  
' Вывод на экран ЖКИ EV3 информацию о кнопках управления таблицей рекордов  
Function DrawLCDHelpTabl()
  LCD.StopUpdate()                                       ' выводим весь экран сразу  
  LCD.Clear()                                            ' Очистили экран
  '                              1234567890123456789012
  LCDRu.TextRu(1,   0,    0, 1, "Pflfqnt bvz buhjrf bkb")     'ЗАДАЙТЕ ИМЯ ИГРОКА ИЛИ
  LCDRu.TextRu(1,   0,   10, 1, "ds,thbnt bp nf,kbws/a,  ")   'ВЫБЕРИТЕ ИЗ ТАБЛИЦЫ,
  LCDRu.TextRu(1,   0,   20, 1, "ddjlz cdjq gfhjkm/a.    ")   'ВВОДЯ СВОЙ ПАРОЛЬ
  LCDRu.TextRu(1,   0,   30, 1, "Ds,jh - hfvrf djrheu/a. ")   'Выбор - рамка вокруг.
  LCDRu.TextRu(1,   0,   40, 1, "/0Dybpe - Yjdsq buhjr/a.")   'Внизу - новый игрок.
  LCDRu.TextRu(1,   0,   50, 1, "======================")     '====================
  LCDRu.TextRu(1,   0,   60, 1, "D nf,kbwt htrjhljd/a:   ")   'В ТАБЛИЦЕ РЕКОРДОВ:
  LCDRu.TextRu(1,   0,   70, 1, "/aU-/rDdth[ gj nf,kbwt")     'U - Вверх по таблице
  LCDRu.TextRu(1,   0,   80, 1, "/aL-/rBpvtybnm bvz    ")     'L - Изменить имя
  LCDRu.TextRu(1,   0,   90, 1, "/aE-/rDs,hfnm buhjrf  ")     'E - Выбрать игрока
  LCDRu.TextRu(1,   0,  100, 1, "/aR-/rBpvtybnm gfhjkm ")     'R - Изменить пароль
  LCDRu.TextRu(1,   0,  110, 1, "/aD-/rDybp gj nf,kbwt ")     'D - Вниз по таблице
  LCD.Update()                                                ' Выводим сразу всю картинку
  Buttons.Wait()                                              ' по нажатию на любую кнопку
  Buttons.Flush()                                             ' выходим из показа экрана
EndFunction

' --------------------------------------------------
' Ввод в интерактивном режиме текущей даты. Задаем dd - день, mm - месяц и yy - год
' после конвертируем в строку "dd.mm.yy"
Function EnterDateRec(in number numIn)
  If numIn <> 0 Then
    dd = EV3File.ConvertToNumber(Text.GetSubText(TableRec[2*numIn], 3, 2))
    mm = EV3File.ConvertToNumber(Text.GetSubText(TableRec[2*numIn], 6, 2))
    yy = EV3File.ConvertToNumber(Text.GetSubText(TableRec[2*numIn], 9, 2))
  EndIf
  ConvertDate() ' текущее значение даты - начинаем с последней запомненной даты в таблице 
  ' Выводим текущую дату на экран и редактируем ее по частям день, месяц, год. Изменяюмую часть берем в квадрат
  ' кнопками "U" или "D" - "+" или "-" увеличиваем или уменьшаем число по кругу, кнопкой "R" или "L" перемещаемся по частям даты
  ' кнопкой "E" - завершаем ввод даты
  LCDRu.Clear()
  LCDRu.TextRu(1, 16, 10, 2, "DDTLB LFNE")             ' Заголовок " ВВЕДИ ДАТУ"
  LCDRu.Text(1, 30, 45, 2, TekDate)                    ' Сама дата
  LCDRu.Rect(  1, 20, 35, 146, 36)                     ' Рамка для даты
  LCDRu.TextRu(1, 0,  75, 1, "RYJGRB/a:")              ' Подсказка    КНОПКИ:
  LCDRu.TextRu(1, 0,  87, 1, "/a U, D - +1, -1 /rR XBCKE")   '  U, D - +1, -1 К ЧИСЛУ
  LCDRu.TextRu(1, 0,  99, 1, "/a L, R - /rDKTDJ // DGHFDJ")     '  L, R - ВЛЕВО / ВПРАВО
  LCDRu.TextRu(1, 0, 111, 1, "/a E    - /rDDJL LFNS      ")     '  E    - ВВОД ДАТЫ
  Buttons.Flush()
  Nd = 0
  btn = ""
  LCDRu.Rect(1, 26 + Nd * 48, 43, 36, 22)    ' рамка вокруг текущей части
  While btn <> "E"
    Buttons.Wait()
    LCDRu.Rect(0, 26 + Nd * 48, 43, 36, 22)  ' убираем рамку вокруг текущей части даты
    btn = Buttons.GetClicks()
    If      btn = "U" Then
      If     Nd = 0 Then
        If dd < 31 Then
          dd = dd + 1
        EndIf
      ElseIf Nd = 1 Then
        If mm < 12 Then
          mm = mm + 1
        EndIf
      ElseIf Nd = 2 Then
        If yy < 99 Then
          yy = yy + 1
        EndIf
      EndIf
    ElseIf  btn = "D" Then
      If     Nd = 0 Then
        If dd > 1 Then
          dd = dd - 1
        EndIf
      ElseIf Nd = 1 Then
        If mm > 1 Then
          mm = mm - 1
        EndIf
      ElseIf Nd = 2 Then
        If yy > 25 Then
          yy = yy - 1
        EndIf
      EndIf
    ElseIf  btn = "L" Then
      If Nd > 0 Then
        Nd = Nd - 1
      EndIf
    ElseIf  btn = "R" Then
      If Nd < 2 Then
        Nd = Nd + 1
      EndIf
    EndIf
    ConvertDate()                           ' новую дату
    LCDRu.Text(1, 30, 45, 2, TekDate)       ' выводим на экран
    LCDRu.Rect(1, 26 + Nd * 48, 43, 36, 22) ' и также рамку вокруг текущей части
  EndWhile
EndFunction

Function ConvertDate()
  TekDate = ""
  If dd < 10 Then
    TekDate = TekDate + "0"
  EndIf
  TekDate = TekDate + dd + "."
  If mm < 10 Then
    TekDate = TekDate + "0"
  EndIf
  TekDate = TekDate + mm + "." + yy
EndFunction